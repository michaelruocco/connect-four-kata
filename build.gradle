plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'com.github.ben-manes.versions' version '0.17.0'
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

group 'com.github.michaelruocco'
version '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    mavenCentral()
}

dependencies {
    compile group:'org.apache.commons', name: 'commons-lang3', version: '3.7'

    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.9.0'
    testCompile group: 'junit', name:'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'

    integrationTestCompile group: 'org.easytesting', name: 'fest-swing', version: '1.2.1'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    executionData test, integrationTest
}

def mainClass = 'com.github.michaelruocco.connectfour.Main'

task allJar(type: Jar) {
    manifest {
        attributes 'Main-Class': mainClass
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task runGui(type: JavaExec) {
    main = mainClass
    classpath = configurations.runtime + sourceSets.main.output
}

task runConsole(type: JavaExec) {
    main = mainClass
    standardInput = System.in
    classpath = configurations.runtime + sourceSets.main.output
    args = [ 'console' ]
}